# Maintainer: Chay Nabors <nabochay@amazon.com>

pkgname='amazon-q-minimal'
pkgver='1.6.0'
pkgrel=1
pkgdesc='Add autocomplete and AI to your existing terminal on macOS & Linux'
arch=('x86_64')
url='https://github.com/aws/amazon-q-developer-cli'
license=('MIT OR Apache-2.0')
provides=('amazon-q')
conflicts=('amazon-q')
makedepends=(cargo)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('3c00eff341f2bf908048be22380e20c79a7d8ec7940a9dbe72de34458a48d65b')
# lto is disabled due to ring issue (https://github.com/briansmith/ring/issues/1444)
options=('!lto')

prepare() {
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --bin q_cli --bin figterm
}

package() {
    install -Dm755 "$srcdir/target/release/q_cli" "$pkgdir/usr/bin/q"
    install -Dm755 "$srcdir/target/release/figterm" "$pkgdir/usr/bin/qterm"
}
